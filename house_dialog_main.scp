[DIALOG d_house_menu]
100,100
page 0
ref1=<src.ctag0.hs_sign> //sign
ref2 = <ref1.link> // house
ref3 = <ref2.owner> //owner
ref4 = <ref2.region.uid>
local.housetype=<ref2.housetype>
if (<ref2.isowner <src>>) || (<src.isgm>)
	local.owner=1
elif (<ref2.GetCoownerPos <src>> >= 0)
	local.coowner=1
elif (<ref2.GetFriendPos <src>> >= 0)
	local.friend=1
endif

fd_hs_bg 100,100
dhtmlgump 125 134 106 60 0 0 <def.center><def.h3><def.bfont_size7><ref2.name>
if <src.account.plevel> > 1
	button 120 120 36 36 1 0 20006 // update dialog debug
endif
if (<local.housetype>==<def.house_guild>)
	ref5=<ref2.tag0.is_guild>
	if <ref5.isvalid>
		if !<isempty <ref5.abbrev>>
			local.guild_name=[<ref5.abbrev>]
		else
			local.guild_name=<def.bfont_size1><ref5.name>
		endif
		local.guild=1
		dhtmlgump 125 118 106 20 0 0 <def.center><local.guild_name>
	endif
endif

dorigin 100 91 // header buttons
for 1 5
	local.opt=0
	if <dlocal._for>==2  // SECURITY
		if <src.dctag0.hs_menu>==<dlocal._for>
			local.opt=1
		elif <src.dctag0.hs_menu>==7 // coowner list
			local.opt=1
		elif <src.dctag0.hs_menu>==17 // remove coowner
			local.opt=1
		elif <src.dctag0.hs_menu>==27 // Clear list confirm
			local.opt=1
		endif
	elif <dlocal._for>==4  // CUSTOMIZE
		if <src.dctag0.hs_menu>==<dlocal._for>
			local.opt=1
		elif <src.dctag0.hs_menu>==20 // Change House Sign
			local.opt=1
		elif <src.dctag0.hs_menu>==21 // Change House Sign Color
			local.opt=1
		elif <src.dctag0.hs_menu>==22 // Change House Sign Hanger
			local.opt=1
		elif <src.dctag0.hs_menu>==23 // Change House Sign Post
			local.opt=1
		endif
	elif <dlocal._for>==5 // OWNERSHIP
		if <src.dctag0.hs_menu>==<dlocal._for>
			local.opt=1
		elif <src.dctag0.hs_menu>==30 // property tax
			local.opt=1
		endif
	elif <src.dctag0.hs_menu>==<dlocal._for>
		local.opt=1
	endif
	if <local.opt>
		dhtmlgump 284 *20 120 20 0 0 <def.bfont_hs_yellow><def0.hs_menu_title_<dlocal._for>>
		button 250 -3 4006 4007 1 0 <eval <dlocal._for>+20000>
	else
		dhtmlgump 284 *20 120 20 0 0 <def.bfont_hs_text1><def0.hs_menu_title_<dlocal._for>>
		button 250 -3 4005 4007 1 0 <eval <dlocal._for>+20000>
	endif
endfor

dorigin 144 508 // bottom buttons
if (<local.owner>) || (<local.coowner>) || (<local.friend>)
	dhtmlgump - - 150 20 0 0 <def.bfont_hs_text1><QVAL (<local.housetype>==<def.house_private>)?Grant Access:Banish>
	button -37 -3 4005 4007 1 0 <QVAL (<local.housetype>==<def.house_private>)? 4022:4032>
	dhtmlgump - *20 150 20 0 0 <def.bfont_hs_text1><QVAL (<local.housetype>==<def.house_private>)?Revoke Access:Lift a Ban>
	button -37 -3 4005 4007 1 0 <QVAL (<local.housetype>==<def.house_private>)? 4021:4031>
	button *316 -3 4005 4007 1 0 4103
	dhtmlgump -52 - 50 20 0 0 <def.div_right><def.bfont_hs_text1>Eject
endif

page 1
if <isempty <src.ctag.hs_menu>>
	src.ctag.hs_menu=1
	call fd_house_menu_1
elif <src.dctag0.hs_menu>==6
	if (<local.housetype>==<def.house_private>)
		call f_house_render_list 4,1 // access list
	else
		call f_house_render_list 3,1 // ban list
	endif
elif <src.dctag0.hs_menu>==7 // coowners list
	call f_house_render_list 1
elif <src.dctag0.hs_menu>==8 // friends list
	call f_house_render_list 2
elif <src.dctag0.hs_menu>==9
	call f_house_render_list 3,1
elif <src.dctag0.hs_menu>==17
	call f_house_render_list 1,1
elif <src.dctag0.hs_menu>==18
	call f_house_render_list 2,1
else
	call fd_house_menu_<src.dctag0.hs_menu>
endif



[DIALOG d_house_menu prebutton]
src.dialogclose d_house_menu
ref1=<src.ctag0.hs_sign> // sign
ref2=<ref1.link>
if !(<ref1.isvalid> && <ref2.isvalid>)
	return 1
endif
serv.log @2,0,0 Pressed <dargn1>

[DIALOG d_house_menu button]
ON=0
	return 1

ON=1 // HOUSE SPEECH COMMANDS MENU
	ref1=<src.ctag0.hs_sign> // sign
	ref2=<ref1.link> // house
	if (<ref2.isowner <src>> || <ref2.GetCoownerPos <src>> >= 0 || <ref2.GetFriendPos <src>> >= 0 || <src.isgm>)
		src.f_resenddialog d_house_commands
	endif
	sdialog d_house_menu

ON=20001 20005 // HEADER BUTTONS
	ref1=<src.ctag0.hs_sign> // sign
	ref2=<ref1.link>
	local.opt=<eval <argn1>-20000>
	if <dlocal.opt> > 3
		if (<ref2.isowner <src>>) || (<ref2.GetCoownerPos <src>> >= 0) || (<ref2.GetFriendPos <src>> >= 0) || (<src.isgm>)
			src.ctag.hs_menu=<dlocal.opt>
		endif
	else
		src.ctag.hs_menu=<dlocal.opt>
	endif
	sdialog d_house_menu

ON=20006
	src.ctag0.dlg_bg ++
	if <src.dctag0.dlg_bg>==<ddef0.dlg_bg_max>
		src.ctag.dlg_bg=
	endif
	sdialog d_house_menu

// **********************************
// **********************************
// **********************************
// SECURITY

ON=4100 // PRIVATE/PUBLIC TOGGLE
	ref1=<src.ctag0.hs_sign> // sign
	ref2=<ref1.link> // house
	local.housetype=<ref2.housetype>
	if (<local.housetype>==<def.house_private>)
		ref2.housetype=<def.house_public>
		if <def.hs_wipe_switch_lists>
			if <ref2.accesses>
				ref2.delaccess -1
				src.sysmessage @1151,,1 Access list cleared.
			endif
		endif
		src.sysmessage @1151,,1 This property is now open to the public (shop setting), only those on the banned list may not enter.
		if ((<def0.hs_door_acces_key>) && (<serv.AutoHouseKeys>)) // If keys are used, let the server check them.
			src.sysmessage @1151,,1 Don't forget to set security level and lock the doors if areas are/need to be restricted.
		else
			src.sysmessage @1151,,1 Don't forget to set security level if areas are/need to be restricted.
		endif
	else
		ref2.housetype=<def.house_private>
		if <def.hs_wipe_switch_lists>
			if <ref2.bans>
				ref2.delban -1
				src.sysmessage @1151,,1 Access list cleared.
			endif
		endif
		src.sysmessage @1151,,1 This propery is now private & may only be accessed by co-owners/friends & those on the access list
	endif
	ref1.resendtooltip 1,0
	sdialog d_house_menu

ON=4101 // GUILD HOUSE TOGGLE
	ref1=<src.ctag0.hs_sign> // sign
	ref2=<ref1.link> // house
	local.housetype=<ref2.housetype>
	if (<local.housetype>==<def.house_guild>)
		ref2.housetype=<def.house_private>
		ref5=<ref2.tag0.is_guild>
		local.guild_name=<qval <isempty <ref5.abbrev>>?<ref5.name>:<ref5.abbrev>>
		src.sysmessage @,,1 This building is no longer <local.guild_name> guildhouse.
		ref2.tag.is_guild=
		ref1.resendtooltip 1,0
		sdialog d_house_menu
	else
		src.sysmessage @,,1 Target your guildstone
		src.targetf f_house_declare_guild <ref2>
	endif

ON=4102 // RECODE DOORS
	src.message @,,1 Target item
	src.targetf fd_house_secure

ON=4103 // EJECT PLAYER
	src.message @42,,1 Target player to eject
	src.targetf f_house_eject

// *****************************
// LISTS
ON=4001 4004 // COOWNER LIST
	ref1=<src.ctag0.hs_sign> // sign
	ref2=<ref1.link>
	if (<dargn1>==4002) // add coowner target
		src.message @,,1 Select player to add as coowner
		src.targetf f_house_add_list 1
	elif !<ref2.coowners>
		src.sysmessage @,,1 The coowner list is empty.
		sdialog d_house_menu
	else
		src.ctag.list=Coowner
		if (<dargn1>==4001) // view coowner list
			src.ctag.hs_menu=7
		elif (<dargn1>==4003) // remove coowner
			src.ctag.hs_menu=17
		elif (<dargn1>==4004) // clear list
			src.ctag.hs_menu=27
		endif
		sdialog d_house_menu
	endif
	
ON=4011 4014 // FRIENDS LIST
	ref1=<src.ctag0.hs_sign> // sign
	ref2=<ref1.link>
	if (<dargn1>==4012) // add friend target
		src.message @,,1 Select player to add as friend
		src.targetf f_house_add_list 2
	elif !<ref2.friends>
		src.sysmessage @,,1 The friend list is empty.
		sdialog d_house_menu
	else
		src.ctag.list=Friend
		if (<dargn1>==4011) // view friend list
			src.ctag.hs_menu=8
		elif (<dargn1>==4013) // remove friend
			src.ctag.hs_menu=18
		elif (<dargn1>==4014) // clear list
			src.ctag.hs_menu=27
		endif
		sdialog d_house_menu
	endif

ON=4021 4023 // ACCESS LIST
	ref1=<src.ctag0.hs_sign> // sign
	ref2=<ref1.link>
	if (<dargn1>==4022) // grant access target
		src.message @,,1 Select player to grant access
		src.targetf f_house_add_list 4
	elif !<ref2.accesses>
		src.sysmessage @,,1 The access list is empty.
		sdialog d_house_menu
	else
		src.ctag.list=Access
		if (<dargn1>==4021) // view/remove access list
			src.ctag.hs_menu=6
		elif (<dargn1>==4023) // clear list
			src.ctag.hs_menu=27
		endif
		sdialog d_house_menu
	endif

ON=4031 4033 // BAN LIST
	ref1=<src.ctag0.hs_sign> // sign
	ref2=<ref1.link>
	if (<dargn1>==4032) // add to ban target
		src.message @,,1 Select player to banish
		src.targetf f_house_add_list 3
	elif !<ref2.bans>
		src.sysmessage @,,1 The ban list is empty.
		sdialog d_house_menu
	else
		src.ctag.list=Ban
		if (<dargn1>==4031) // view/remove access list
			src.ctag.hs_menu=9
		elif (<dargn1>==4033) // clear list
			src.ctag.hs_menu=27
		endif
		sdialog d_house_menu
	endif


ON=4200 4399 // REMOVE FROM LIST
	ref1=<src.ctag0.hs_sign> // sign
	ref2 = <ref1.link> // house
	local.housetype=<ref2.housetype>
	local.list=<src.ctag0.list>
	local.opt=<eval <argn1>-4200>
	if <ref2.<local.list><qval (strmatch(*access*,<local.list>))?es:s>>
		ref4=<ref2.<local.list>.<dlocal.opt>> // player in list
		if (<ref4>==<src>)
			ref5=<src>
			src.sysmessage @,,1 You have removed yourself from the <local.list> list.
		else
			ref5=<ref4>
			src.sysmessage @,,1 <ref5.name> has been removed from the <local.list> list.
		endif
		if (<local.housetype>==<def.house_private>) && (<ref5.region.uid>==<ref2.region.uid>)
			ref5.go=<ref1.p>
			ref5.sysmessage @33,,1 This is private property, you no longer have permission to enter.
		endif
		if (<ref2.<local.list><qval (strmatch(*access*,<local.list>))?es:s>> < 2 )
			ref2.del<local.list> -1
		else
			ref2.del<local.list> <ref2.<local.list>.<dlocal.opt>.uid>
		endif
	endif
	sdialog d_house_menu

ON=4400 // HOUSE LIST CLEAR
	ref1=<src.ctag0.hs_sign> // sign
	ref2=<ref1.link> // house
	local.list=<src.ctag0.list>
	if <ref2.<local.list><qval (strmatch(*access*,<local.list>))?es:s>>
		ref2.del<local.list> -1
		src.sysmessage @,,1 The <local.list> list has been cleared.
	endif
	src.ctag0.hs_menu=2
	sdialog d_house_menu

// **********************************
// **********************************
// **********************************
// STORAGE

ON=8001 8004
	ref1=<src.ctag0.hs_sign> // sign
	ref2=<ref1.link> // house
	if (<dargn1>==8001) // buy 25% storage
		local.value = <QVAL (<ref2.value>)? <eval (<ref2.value>/8)>:<eval (<ref2.tag0.value>/8)>>
		f_house_hs_buy_storage <dlocal.value>
	elif (<dargn1>==8002) // buy 50% storage
		local.value = <QVAL (<ref2.value>)? <eval (<ref2.value>/4)>:<eval (<ref2.tag0.value>/4)>>
		f_house_hs_buy_storage <dlocal.value>
	elif (<dargn1>==8003) // buy 75% storage
		local.value = <QVAL (<ref2.value>)? <eval ((<ref2.value>/8)*3)>:<eval ((<ref2.tag0.value>/8)*3)>>
		f_house_hs_buy_storage <dlocal.value>
	elif (<dargn1>==8004) // buy 100% storage
		local.value = <QVAL (<ref2.value>)? <eval <ref2.value>/2>:<eval <ref2.tag0.value>/2>>
		f_house_hs_buy_storage <dlocal.value>
	endif
	sdialog d_house_menu

// **********************************
// **********************************
// **********************************
// CUSTOMIZE

ON=7001 7004
	if (<dargn1>==7001) // Change House Sign
		src.ctag.hs_menu=20
	elif (<dargn1>==7002) // Change House Sign Color
		src.ctag.hs_menu=21
	elif (<dargn1>==7003) // Change House Sign Hanger
		src.ctag.hs_menu=22
	elif (<dargn1>==7004) // Change House Sign POST
		src.ctag.hs_menu=23
	endif
	sdialog d_house_menu

ON=7010
	ref1=<src.ctag0.hs_sign>
	ref1.color=0
	src.sysmessage @1151,,1 The sign color has been reset.
	sdialog d_house_menu

ON=7005 // toggle rename
	src.ctag.hs_rename=1
	sdialog d_house_menu

ON=7006 // RENAME HOUSE
	ref1=<src.ctag0.hs_sign>
	ref2=<ref1.link>
	if (<isempty <argtxt[1]>>)
		src.sysmessage @33,,1 The name was empty.
	elif (<isnum <argtxt[1]>>)
		src.sysmessage @33,,1 That name is invalid (number).
	elif <eval strlen(<argtxt[1]>)> < 2
		src.sysmessage @33,,1 That name is too short (min 2).
	elif <eval strlen(<argtxt[1]>)> > <ddef0.hs_name_lenght_max>
		src.sysmessage @33,,1 That name is too long, <ddef0.hs_name_lenght_max> chars max (<eval strlen(<args>)>).
	elif !(strcmpi(<argtxt[1]>,<ref2.name>))
		src.sysmessage @,,1 Same name.
	else
		local.name=<argtxt[1]>
		src.sysmessage @1151,,1 New property name has been set.
		ref2.name=<local.name>
		ref1.name=<local.name>
		ref1.resendtooltip 1,0
		// if <def.hs_can_decay>
		// 	ref1.attr = 02|010
		// 	if (<ref1.timer> < 0)
		// 		ref1.timer = 60*60*24*<ddef0.hs_can_decay>
		// 	endif
		// else
		// 	ref1.timer = -1
		// 	ref1.attr = 010
		// endif
	endif
	sdialog d_house_menu

ON=2965 3140	// sign dispid
	ref1=<src.ctag0.hs_sign> // sign
	ref1.dispid=<hval <argn1>>
	ref1.update
	src.sysmessage @1151,,1 The sign has been updated.
	sdialog d_house_menu

ON=7100 7200 // sign coloring
	ref1=<src.ctag0.hs_sign> // sign
	if <def.hs_allow_sign_color>
		ref1.color=<def0.sign_color_<eval <argn1>-7100>>
		src.sysmessage @1151,,1 The sign color has been updated.
	endif
	sdialog d_house_menu

ON=7301 7328 // change sign post
	ref1=<src.ctag0.hs_sign> // sign
	ref2=<ref1.link>
	local.opt=<eval <argn1>-7300>
	foritems 2
	if (<baseid>==i_wall_darkwood) && (<link>==<ref2>)
		dispid=<def0.signpost_<dlocal.opt>>
		update
	endif
	endfor
	src.sysmessage @1151,,1 The sign post has been updated.
	sdialog d_house_menu

ON=7701 7712 // sign hanger
	ref1=<src.ctag0.hs_sign> // sign
	ref2=<ref1.link>
	local.opt=<eval <argn1>-4734>
	if (<ref2.comp.2.uid>==<ref1>)
		ref4=<ref2.comp.0.uid>
		ref4.dispid=<hval <local.opt>>
		ref4.update
		src.sysmessage @1151,,1 The sign hanger has been updated.
	endif
	sdialog d_house_menu

ON=7800 // Customize This House
	ref1=<src.ctag0.hs_sign> // sign
	ref2=<ref1.link>
	if (<ref2.tag0.demolition>)
		src.sysmessage @33,,1 This house is scheduled for demolition and cannot be customized.
	elif (<src.region.uid>!=<ref2.region.uid>)
		src.sysmessage @33,,1 You must be within the property.
	elif (<src.housedesign>)
		src.sysmessage @33,,1 You are already designing <QVAL (<src.housedesign.uid>==<ref2>)? this:another> building.
	else
		if !(<src.isevent.e_house_customize>)
			src.events=+e_house_customize
		endif
		ref2.customize <src>
	endif

ON=7801 // Convert into Customizable House
	ref1=<src.ctag0.hs_sign> // sign
	ref2=<ref1.link>
	if (<ref2.tag0.demolition>)
		src.sysmessage @33,,1 This house is scheduled for demolition and cannot be converted.
	else
		try ref2.f_house_swap
	endif

ON=7802 // Convert into Customizable House
	src.sysmessage @,,1 Not implemented at the moment.
	sdialog d_house_menu

ON=7803 // Relocate Moving Crate
	ref1=<src.ctag0.hs_sign> // sign
	ref2=<ref1.link>
	if <ref2.MovingCrate>
		ref3=<ref2.MovingCrate>
		ref3.p = <src.p>
		ref3.attr &= ~attr_invis
		src.update
		src.events=+e_moving_crate
	endif
	sdialog d_house_menu


// **********************************
// **********************************
// **********************************
// OWNERSHIP

on=6001 // Pay Property Tax menu
	src.ctag.hs_menu=30
	sdialog d_house_menu

on=6002 // Put this house up for sale
	src.ctag.hs_menu=31
	sdialog d_house_menu

on=6003 // sell Property
	ref1=<src.ctag0.hs_sign> // sign
	ref2=<ref1.link>
	if (<ref2.tag0.demolition>)
		src.sysmessage @33,,1 This house is scheduled for demolition and cannot be sold.
	elif (<ref2.tag0.forsale>)
		ref2.tag.forsale=
		ref2.tag.price=
		ref1.resendtooltip 1,0
		src.sysmessage @,,1 You have taken this house off the market.
		src.sysmessage @,,1 Don't forget to reset the house settings to your preferences.
	else
		local.gold=<argtxt[0]>
		if !(<isnum <local.gold>>) || (<dlocal.gold> <= 0)
			src.sysmessage @,,1 Only positive numeric values are valid.
		elif (<dlocal.gold> < <ref2.dtag0.value>)
			src.sysmessage @33,,1 Price cannot be lower thant property base value.
		elif (<ref2.lockdowns>) || (<ref2.GetSecuredItems>)
			src.sysmessage @33,,1 You can't put a house on the market as long it has lockdowns/secure containers.
		else
			src.sysmessage @76,,1 Your house is now on the market for <f_numformat <dlocal.gold>> gp.
			ref2.tag.price=<dlocal.gold>
			ref2.tag.forsale=1
			ref2.f_house_open
			// ref2.housetype = <def.house_public>
			if (<def.hs_can_decay>)
				if (<ref2.tag0.decay_exempt>)
					ref2.tag.decay_exempt=
					ref1.attr = 02|010
					ref1.timer = 60*60*24*<ddef0.hs_can_decay>
					src.sysmessage @33,,1 Your property decay timer has been set to <f_time_format <ref1.timer>>.
				endif
			else
				ref1.attr = 010
				ref1.timer = -1
			endif
			ref1.resendtooltip 1,0
		endif
	endif
	sdialog d_house_menu

on=6004 // property Removal menu
	src.ctag.hs_menu=32
	sdialog d_house_menu

on=6010 // PAY TAX
	ref1=<src.ctag0.hs_sign>
	ref2=<ref1.link>
	ref3=<ref2.owner>
	local.h=<ref3.houses>
	local.daysleft = <eval(((<ref1.timer>/60)/60)/24)>
	local.days = <eval(<ddef0.hs_can_decay>-<dlocal.daysleft>)>
	local.value = <eval <ref2.dtag0.value>+<ref1.dtag0.boughtstorage>>
	if !<src.isgm>
		if <def.hs_estate_tax>
			if <local.h>
				if (<dlocal.h> > <ddef0.hs_estate_tax>)
					if (<ddef0.hs_estate_calc>==1)
						if <ref2.region.tag0.maintenance_override>
							local.rate = <eval(<ref2.region.dtag0.maintenance_override>*2)>
							local.maintenance = <MULDIV <dlocal.value>,<dlocal.rate>,100>
						else
							local.rate = <eval(<ddef0.hs_property_tax>+<ddef0.hs_estate_rate>)>
							local.maintenance = <MULDIV <dlocal.value>,<dlocal.rate>,100>
						endif
					elif (<ddef0.hs_estate_calc>==2)
						local.overage = <eval(<dlocal.h>-<ddef0.hs_estate_tax>)>
						if <ref2.region.tag0.maintenance_override>
							local.rate = <eval(<ref2.region.dtag0.maintenance_override>*<dlocal.overage>)>
							local.rate = <eval(<local.rate>+<ref2.region.dtag0.maintenance_override>)>
							local.maintenance = <MULDIV <dlocal.value>,<dlocal.rate>,100>
						else
							local.rate = <eval(<ddef0.hs_estate_rate>*<dlocal.overage>)>
							local.rate = <eval(<local.rate>+<def.hs_property_tax>)>
							local.maintenance = <MULDIV <dlocal.value>,<dlocal.rate>,100>
						endif
					endif
				else
					if <ref2.region.tag0.maintenance_override>
						local.maintenance = <MULDIV <dlocal.value>,<ref2.region.dtag0.maintenance_override>,100>
					else
						local.maintenance = <MULDIV <dlocal.value>,<ddef0.hs_property_tax>,100>
					endif
				endif
			else
				if <ref2.region.tag0.maintenance_override>
					local.maintenance = <MULDIV <dlocal.value>,<ref2.region.dtag0.maintenance_override>,100>
				else
					local.maintenance = <MULDIV <dlocal.value>,<ddef0.hs_property_tax>,100>
				endif
			endif
		else
			if <ref2.region.tag0.maintenance_override>
				local.maintenance = <MULDIV <dlocal.value>,<ref2.region.dtag0.maintenance_override>,100>
			else
				local.maintenance = <MULDIV <dlocal.value>,<ddef0.hs_property_tax>,100>
			endif
		endif
		local.maintenance = <eval(<local.maintenance>/30)>
		local.maintpay = <eval(<dlocal.maintenance>*<dlocal.days>)>
		if (<eval(((<ref1.timer>/60)/60)/24)> < <ddef0.hs_can_decay>)
			if (<src.gold> < <dlocal.maintpay>)
				src.sysmessage @33,,1 You lack the funds to pay the property tax.
				sdialog d_house_menu
				return 1
			endif
			src.gold -= <dlocal.maintpay>
			ref1.timer += <eval((((<dlocal.days>-1)*60)*60)*24)>
			src.sysmessage @1151,,1 <dlocal.maintpay> gp has been paid to cover <dlocal.days> days.
			src.sysmessage @1151,,1 Your property tax is now paid for the next <ddef0.hs_can_decay> days.
		else
			src.sysmessage @33,,1 You cannot pay more property tax yet.
		endif
	else
		ref1.timer = <eval(((<ddef0.hs_can_decay>*24)*60)*60)>
		serv.log <src.name>(<src.account>)[<src>] update <ref2.name>[<ref2>] decay timer of owner <ref3.name>(<ref3.account>)[<ref3.uid>]
		src.sysmessage @1151,,1 The house decay timer has been updated by a staff.
	endif
	sdialog d_house_menu


ON=6011 // schedule demolition GM only
	if <src.isgm>
		ref1=<src.ctag0.hs_sign> // sign
		ref2=<ref1.link>
		if (<ref2.tag0.demolition>)
			ref2.tag.demolition=
			ref1.resendtooltip 1,0
			src.sysmessage @1151,,1 The scheduled demolition has been cancelled.
		else
			local.days=<argtxt[1]>
			if !(<isnum <local.days>>)
				src.sysmessage @33,,1 Invalid entry, days must be numeric (7-30)
			elif (<dlocal.days> > 30)
				src.sysmessage @33,,1 Demolition must occur within 30 days.
			elif (<dlocal.days> < 7)
				src.sysmessage @33,,1 The minimum notice period before demolition can occur is 7 days.
			else
				ref3=<ref2.owner> // owner
				if (<ref2.tag0.forsale>)
					ref2.tag.forsale=
					ref2.tag.price=
					src.sysmessage @,,1 Your property is no longer for sale.
				endif
				ref2.tag.demolition=1
				ref1.timer = 60*60*24*<dlocal.days>
				ref1.resendtooltip 1,0
				src.sysmessage @1151,,1 You have scheduled this house to be removed in <dlocal.days> days.
				if <ref3.isonline>
					ref3.sysmessage @33,,1 Your property <ref2.name> has been scheduled for removal in <dlocal.days> days.
				endif
			endif
		endif
	endif
	sdialog d_house_menu

ON=6012 // set decay exempt GM only
	if <src.isgm>
		ref1=<src.ctag0.hs_sign> // sign
		ref2=<ref1.link>
		ref2.tag.decay_exempt=1
		ref1.attr &= ~02
		ref1.timer=-1
		src.sysmessage @1151,,1 Your property is now exempt from decay.
	endif
	sdialog d_house_menu

ON=6013 // remove decay exempt GM only
	if <src.isgm>
		ref1=<src.ctag0.hs_sign> // sign
		ref2=<ref1.link>
		ref2.tag.decay_exempt=
		ref1.attr |= 02
		ref1.timer = 60*60*24*<ddef0.hs_can_decay>
		src.sysmessage @1151,,1 Your property will decay in <f_time_format <ref1.timer>>.
	endif
	sdialog d_house_menu

ON=6014 // transfer ownership
	ref1=<src.ctag0.hs_sign>
	ref2=<ref1.link>
	if (<ref2.tag0.demolition>)
		src.sysmessage @33,,1 This house is scheduled for demolition and cannot be traded.
		sdialog d_house_menu
	else
		src.message @,,1 To whom do you wish to transfer this property?
		src.targetf f_house_transfer <ref2>
	endif

on=6015 // DEMOLISH HOUSE
	ref1=<src.ctag0.hs_sign> // sign
	ref2=<ref1.link>
	local.housetype=<ref2.housetype>
	if (<local.housetype>==<def.house_guild>)
		src.sysmessage @33,,1 This is a guild house.
		src.sysmessage @,,1 You must remove the guild declaration and move the guildstone before you can demolish the building.
		return 1
	endif
	ref3=<ref2.owner> // owner
	local.total=<eval((<ref2.value>+<ref2.dtag0.boughtstorage>)+<ref2.dtag0.construction>)>
	src.sysmessage @,,1 You spent <f_numformat <dlocal.total>> gp for upgrades on this property.
	if <def.hs_demolish_fee>
		local.demolishfee = <muldiv <dlocal.total>,<ddef.hs_demolish_fee>,100>
		local.total -= <dlocal.demolishfee>
		src.sysmessage @,,1 Your demolish fee of <f_numformat <dlocal.demolishfee>> gp was deducted from refund.
	endif
	if <ref2.isowner <src>>
		local.owner=1
		src.gold += <dlocal.total>
		src.sysmessage @,,1 You have been refunded <f_numformat <dlocal.total>> gp.
	else
		ref3.gold += <dlocal.total>
		src.sysmessage @,,1 <ref3.name> was refunded <f_numformat <dlocal.total>> gp.
	endif
	ref2.f_house_remove_allitems
	ref2.remove
	if (<local.owner>)
		src.sysmessage @,,1 The number of houses you own is now <ref3.houses>.
	else
		src.sysmessage @,,1 The number of houses <ref3.name> owns is now <ref3.houses>.
	endif
	src.region.allclients.update
	src.ctag.hs_sign=
	src.ctag.hs_menu=

on=6016 // REDEED HOUSE
	ref1=<src.ctag0.hs_sign> // sign
	ref2=<ref1.link>
	local.housetype=<ref2.housetype>
	if (<local.housetype>==<def.house_guild>)
		src.sysmessage @33,,1 This house is a guild house.
		src.sysmessage @,,1 You must remove the guild declaration and move the guildstone before you can redeed the building.
		sdialog d_house_menu
		return 1
	endif
	if (<ref2.type>==t_multi_custom)
		local.total = <eval((<ref2.value>+<ref2.dtag0.boughtstorage>)+<ref2.dtag0.construction>)>
		src.sysmessage @,,1 You spent <dlocal.total> gp for upgrades on this property.
		if <def.hs_redeed_fee>
			local.redeedfee = <qval <src.isgm>?0:<muldiv <dlocal.total>,<ddef.hs_redeed_fee>,100>>
			local.total -= <dlocal.redeedfee>
			src.sysmessage @,,1 Your redeed fee was <dlocal.redeedfee> gp.
		endif
		src.gold += <dlocal.total>
		ref2.remove
		src.sysmessage @,,1 You have been refunded <dlocal.total> gp.
	elif (<ref2.type>==t_multi)
		local.total = <eval(<ref2.value>+<ref2.dtag0.boughtstorage>)>
		local.redeedfee = <qval <src.isgm>?0:<muldiv <dlocal.total>,<ddef.hs_redeed_fee>,100>>
		local.refund = <ref2.dtag0.boughtstorage>
		if <def.hs_redeed_fee>
			if (<dlocal.refund> > <dlocal.redeedfee>)
				src.sysmessage @,,1 You spent <dlocal.refund> gp for upgrades on this property.
				local.refund -= <dlocal.redeedfee>
				src.gold += <dlocal.refund>
				src.sysmessage @,,1 Your redeed fee of <dlocal.redeedfee> gp was deducted from refund.
				src.sysmessage @,,1 You have been refunded <dlocal.refund> gp.
			else
				local.redeedfee -= <dlocal.refund>
				if (<src.gold> < <dlocal.redeedfee>)
					src.sysmessage @33,,1 You lack the funds to redeed this property.
					sdialog d_house_menu
					return 1
				else
					if !<src.isgm>
						src.gold -= <dlocal.redeedfee>
						src.sysmessage @33,,1 <dlocal.redeedfee> gp has been deducted from your balance to pay for this redeed.
					endif
				endif
			endif
		else
			src.gold += <dlocal.refund>
			src.sysmessage @33,,1 You have been refunded <dlocal.refund> gp.
		endif
		if (<ddef0.hs_redeed_crate>==1)
			ref2.f_house_box
		endif
		ref2.redeed
		src.sysmessage @,,1 The number of houses you own is now <src.houses>.
	endif
	src.region.allclients.update



[EOF]